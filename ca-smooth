#!/usr/bin/env perl
#
# cellular automaton smooth a map supplied on stdin

use 5.24.0;
use warnings;

my @field;
while (readline) {
    chomp;
    # TWEAK this is how characters become # (on) or . (off)
    tr/+X,Pf /.#..../;
    push @field, [ split // ];
}

sub living {
    my ( $field, $r, $c ) = @_;
    my $rows = $field->$#*;
    my $cols = $field->[0]->$#*;
    my $living = 0;
    for my $i ( -1, 1 ) {
        my $x = $c + $i;
        if ( $x >= 0 and $x <= $cols ) {
            $living++ if $field->[$r][$x] eq '#';
        }
        for my $j ( -1 .. 1 ) {
            $x = $r + $i;
            my $y = $c + $j;
            if ( $x >= 0 and $x <= $rows and $y >= 0 and $y <= $cols ) {
                $living++ if $field->[$x][$y] eq '#';
            }
        }
    }
    return $living;
}

my $unstable;
my $iters;
do {
    $unstable = 0;
    my $cols = $field[0]->$#*;
    for my $r ( 0 .. $#field ) {
        for my $c ( 0 .. $cols ) {
            my $l = living \@field, $r, $c;
            if ( $field[$r][$c] eq '#' ) {
                if ( $l < 3 ) {
                    $field[$r][$c] = '.';
                    $unstable = 1;
                }
            } else {
                if ( $l >= 6 ) {
                    $field[$r][$c] = '#';
                    $unstable = 1;
                }
            }
        }
    }
    $iters++;
} while $unstable;

for my $row (@field) { say join '', @$row }
