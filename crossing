#!/usr/bin/env perl
#
# "Dijkstra Map" path-finding, illustrated

use 5.24.0;
use warnings;
use List::Util qw(reduce shuffle);
use Game::DijkstraMap 0.08;
#use Time::HiRes qw(sleep);

# PORTABILITY - xterm control sequences - http://invisible-island.net/xterm/
sub at ($$) { "\e[" . ( 1 + $_[0] ) . ";" . ( 1 + $_[1] ) . "H" }
sub clear_screen () { "\e[1;1H\e[2J" }
sub t_hili ()       { "\e[41m" }
sub t_norm ()       { "\e[m" }

my $mapstr    = do { local $/; readline *DATA };
my $Level_Map = Game::DijkstraMap->str2map($mapstr);
my $default   = Game::DijkstraMap->new( map => $Level_Map );

# treat Plants as walls; this could also be done with a custom costfn.
# reveals the square move bias of the normalize_costs() method
my $plevel = $mapstr;
$plevel =~ tr/P/#/;
my $nopath = Game::DijkstraMap->new( str2map => $plevel );

# fungus (and Plants) are the anti-goals in this map
my $flevel = $mapstr;
$flevel =~ tr/@./x/;
my $fungus = Game::DijkstraMap->new( str2map => $flevel );

sub show_path {
    for my $point ( $_[0]->@* ) {
        say at( $point->[0], $point->[1] ), t_hili,
          $Level_Map->[ $point->[0] ][ $point->[1] ], t_norm;
        #sleep 0.1;
    }
}

sub show_disjoint {
    for my $point ( $_[0]->unconnected->@* ) {
        say at( $point->[0], $point->[1] ), t_hili,
          $Level_Map->[ $point->[0] ][ $point->[1] ], t_norm;
    }
}

sub bestpath {
    my $path = $_[0]->path_best( 0, 0 );
    return show_disjoint $_[0] unless $path->@*;
    show_path($path);
}

# TODO not really practical, gets stuck as local minimum...
sub avoid_fungus {
    my ( $dm, $fm ) = @_;
    my ( $r, $c ) = ( 0, 0 );
    my @path;
    my $i = 100;
    while ( my $choice =
        $dm->next_with( $r, $c, { objs => [$fm], weights => [3] } ) ) {
        push @path, $choice;
        ( $r, $c ) = $choice->@*;
        last if $i-- == 0;
    }
    die "no path found\n" if !@path;
    show_path( \@path );
}

sub a_map {
    say clear_screen, t_norm, $mapstr;

    # TWEAK fiddle with these...
    #avoid_fungus $default, $fungus;
    bestpath $default;
    #bestpath $nopath;

    say at $Level_Map->@*, 0;
}

# these are best with a smaller test map whose TSV fit in the terminal
#say $default->to_tsv($Level_Map);
#say $default->to_tsv(
#    $default->dimap_with(
#        {   objs      => [$fungus],
#            weights   => [3],
#            my_weight => 1
#        }
#    )
#);
a_map

__DATA__
@..f..f.........f.f.#.##.f..P............##......................#######
ff....ff.ff.P...f.fff###f..f.f......f.##...#......................#.##.#
..fff..f.....f##.ff.#####...f.f.P....ff######........f...........##.##..
.....f.......#.##..##Pf#.#.....fff........#..#f....f..#..........#f#...f
.f...........##.####.....#.f...fff..........P#..f#P..#............##....
.........f......###....f.f#.f.f.....##........#..f###..f...f.....###f..x
f..........##...##.#........ff.......P#.......f..f.f..ff.f...f...##..f..
........ff.#fff.####.f....ffff.f......##...............ff.f..f...###f...
f....#..f.#.ff#..#ff...f.fffff.......##.#..P..f.f..ff.f..f.f......##...f
.#.####.##.ff.#####.ff.fff..ff.......f.####.###...f..f.f.f.....#.###f...
###..#.##P.f.#####....f..#..f......f....PP###..#..ff.fff...fff.####.....
..f#....##..##....f.....###f.....ff.....f.#####.#......f..f.P..####...PP
...###....f#.#f..P.f..######....f.f..fff.#....###...f..f...##f.#.#.P....
f..f#f#.....####f.f####...#.....f.fff...#...f.####.........#ffff......P.
f.f..#.#..f####.#ff.##.........f..f.f.f.f.P.#####.........#..ff.....###.
f.ff.##....#####f.f..f#.......f..ff.f..f.f.f..##.............##...#..#..
f.f..###.f..#f.#.f...#......f.fff.f...fffff.f##..............##..#######
..fff.f##f.#...P.f.........####.f##.f.fff...f#..................#######.
.......f.P#..f............###f.####..ff.ff#f.#...................##..#..
f....fff............f....ff###........fff.####.f......................P.
